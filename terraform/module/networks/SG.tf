# Security Group for EKS Cluster
resource "aws_security_group" "eks_clustersg" {
  name_prefix = "${var.project_name}-eks-cluster-sg"
  vpc_id      = aws_vpc.eks_vpc.id

  ingress {
    description = "HTTPS"
    from_port   = 443
    to_port     = 443
    protocol    = "tcp"
    cidr_blocks = [var.vpc_cidr]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = {
    Name = "${var.project_name}-eks-cluster-sg"
  }
}

# Security Group for EKS Node Group
resource "aws_security_group" "eks_nodesg" {
  name_prefix = "${var.project_name}-eks-node-sg"
  vpc_id      = aws_vpc.eks_vpc.id

  ingress {
    description = "Allow nodes to communicate with each other"
    from_port   = 0
    to_port     = 65535
    protocol    = "tcp"
    self        = true
  }

  ingress {
    description     = "Allow worker Kubelets and pods to receive communication from the cluster control plane"
    from_port       = 1025
    to_port         = 65535
    protocol        = "tcp"
    security_groups = [aws_security_group.eks_clustersg.id]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = {
    Name = "${var.project_name}-eks-node-sg"
  }
}

# Allow inbound traffic from EKS Node Group to EKS Cluster
resource "aws_security_group_rule" "cluster_inbound" {
  description              = "Allow worker nodes to communicate with the cluster API Server"
  from_port                = 443
  protocol                 = "tcp"
  security_group_id        = aws_security_group.eks_clustersg.id
  source_security_group_id = aws_security_group.eks_nodesg.id
  to_port                  = 443
  type                     = "ingress"
}

output "securitygroup" {
  description = "Security group ID attached to the EKS cluster"
  value       = aws_security_group.eks_clustersg.id
}